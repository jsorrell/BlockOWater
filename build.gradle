buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '2.3-SNAPSHOT', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

loadProperties()

version = config.mod_version
group = 'com.jsorrell.blockowater'
archivesBaseName = 'BlockOWater'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

def loadProperties() {
    // Config file with custom properties
    ext.configFile = file "build.properties"
    configFile.withReader {
        def prop = new Properties()
        prop.load(it)
        ext.config = new ConfigSlurper().parse prop
    }

    // grab buildNumber
    ext.buildnumber = "DEV" // this will be referenced as simply project.buildnumber from now on.
    if (System.getenv().BUILD_NUMBER)
        project.buildnumber = System.getenv().BUILD_NUMBER
    logger.lifecycle "BUILDING VERSION: " + project.buildnumber
}

minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"
    runDir = "run";
    mappings = "${config.mcp_mappings_channel}_${config.mcp_mappings_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

jar {
    manifest {
        attributes([
                "Specification-Title": "Block O' Water",
                "Specification-Vendor": "jsorrell",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" :"jsorrell",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
}

// Fixes IntelliJ bug that prevents assets from working
// https://stackoverflow.com/a/27624502
sourceSets {
    main { output.resourcesDir = output.classesDir }
}
